import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id 'com.modrinth.minotaur'
    id 'net.darkhax.curseforgegradle'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

loom {
    def aw = file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

modrinth {
    token = findProperty("modrinth.key") ?: "Invalid/No API Token Found"
    projectId = modrinth_id
    versionNumber.set(project.version.toString())
    versionName = "Fabric ${minecraft_version}"
    uploadFile.set(tasks.remapJar)
    changelog.set(rootProject.file("changelog.txt").getText("UTF-8"))
    gameVersions.set([minecraft_version])
    versionType = "release"
    loaders.set(["fabric"])
    dependencies {
        required.project("fabric-api")
    }

    //debugMode = true
    //https://github.com/modrinth/minotaur#available-properties
}

tasks.register("publishToCurseForge", TaskPublishCurseForge) {
    group = "publishing"
    apiToken = findProperty("curseforge.apitoken") ?: "Invalid/No API Token Found"

    def mainFile = upload(curseforge_id, tasks.remapJar)
    mainFile.releaseType = "release"
    mainFile.addModLoader("Fabric")
    mainFile.addGameVersion(minecraft_version)
    mainFile.addJavaVersion("Java 21")
    mainFile.changelog = rootProject.file("changelog.txt").getText("UTF-8")

    //debugMode = true
    //https://github.com/Darkhax/CurseForgeGradle#available-properties
}